
&НаКлиенте
Процедура ЗаполнитьПоДаннымМосбиржи(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Код) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для заполнения необходимо указать Код!",, "Объект.Код");
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьПоДаннымМосбиржиНаСервере();
	
КонецПроцедуры	
	
&НаСервере
Процедура ЗаполнитьПоДаннымМосбиржиНаСервере()
		
	СведенияОбОблигации = ПолучениеДанныхМосбиржиСервер.ПолучитьСведенияОбОблигацииССайтаМосбиржи(Объект.Код);
	Если СведенияОбОблигации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураОбъект = РеквизитФормыВЗначение("Объект");
	НоменклатураОбъект.ЗаполнитьПоСведениямОбОблигации(СведенияОбОблигации);
	ЗначениеВРеквизитФормы(НоменклатураОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияПриИзменении(Элемент)
	
	СтавкиНалога = ОбщегоНазначенияКлиентСервер.ПолучитьСтавкиНалога(Объект.Категория);
	Объект.СтавкаНалогаНаКупонныйДоход = СтавкиНалога.СтавкаНалогаНаКупонныйДоход;
	Объект.СтавкаНалогаНаПриростКапитала = СтавкиНалога.СтавкаНалогаНаПриростКапитала;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСоответствиеДаннымМосбиржи(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Код) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для проверки соответствия должен быть заполнен Код!",, "Объект.Код");
		Возврат;
	КонецЕсли; 
	
	Расхождения = ПолучитьРасхожденияНаСервере();
	Если Расхождения.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Расхождений с данными Мосбиржи не обнаружено!");
	Иначе 
		ПоказатьЗначение(, Расхождения);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРасхожденияНаСервере()

	СведенияОбОблигации = ПолучениеДанныхМосбиржиСервер.ПолучитьСведенияОбОблигацииССайтаМосбиржи(Объект.Код);
	Если СведенияОбОблигации = Неопределено Тогда
		Результат = Новый Массив; 
		Результат.Добавить("Не удалось получить сведения об этой бумаге с сайта Мосбиржи");
		Возврат Результат;
	КонецЕсли;
	
	НоменклатураОбъект = РеквизитФормыВЗначение("Объект");
	Возврат НоменклатураОбъект.ПолучитьРасхожденияСоСведениямиОбОблигации(СведенияОбОблигации);
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка)
		И Параметры.Свойство("СведенияОбОблигации") Тогда
		СведенияОбОблигации = Параметры.СведенияОбОблигации;
		НоменклатураОбъект = РеквизитФормыВЗначение("Объект");
		НоменклатураОбъект.ЗаполнитьПоСведениямОбОблигации(СведенияОбОблигации);
		ЗначениеВРеквизитФормы(НоменклатураОбъект, "Объект");
	КонецЕсли; 
	
КонецПроцедуры
