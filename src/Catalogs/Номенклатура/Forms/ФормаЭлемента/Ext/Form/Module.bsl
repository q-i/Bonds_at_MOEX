
&НаКлиенте
Процедура ЗаполнитьССайтаМосбиржи(Команда)
	
	КодБумаги = Объект.Код;
	Если НЕ ЗначениеЗаполнено(КодБумаги) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для заполнения необходимо указать Код!",, "Объект.Код");
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьССайтаМосбиржиНаСервере(КодБумаги);
	
КонецПроцедуры	
	
&НаСервере
Процедура ЗаполнитьССайтаМосбиржиНаСервере(КодБумаги)
		
	СведенияОбОблигации = ПолучениеДанныхМосбиржиСервер.ПолучитьСведенияОбОблигацииССайтаМосбиржи(КодБумаги);
	Если СведенияОбОблигации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, СведенияОбОблигации,, "Код, Купоны, Погашения");
	
	ИменаТабЧастей = Новый Массив;
	ИменаТабЧастей.Добавить("Купоны");
	ИменаТабЧастей.Добавить("Погашения");
	Для Каждого ИмяТабЧасти Из ИменаТабЧастей Цикл
		ТабЧасть = Объект[ИмяТабЧасти];
		ТаблицаСведений = СведенияОбОблигации[ИмяТабЧасти];
		ТабЧасть.Очистить();
		Для Каждого СтрокаТаблицы Из ТаблицаСведений Цикл
			НоваяСтрока = ТабЧасть.Добавить(); 
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		КонецЦикла; 
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияПриИзменении(Элемент)
	
	СтавкиНалога = ОбщегоНазначенияКлиентСервер.ПолучитьСтавкиНалога(Объект.Категория);
	Объект.СтавкаНалогаНаКупонныйДоход = СтавкиНалога.СтавкаНалогаНаКупонныйДоход;
	Объект.СтавкаНалогаНаПриростКапитала = СтавкиНалога.СтавкаНалогаНаПриростКапитала;
	
КонецПроцедуры
