
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// значения по умолчанию
	ДатаЗаявок = ТекущаяДатаСеанса();
	ТоргиВРежимеТПлюс = 1; // Т+1
	КомиссияБрокераПроцент = 0.06; // %
	КолвоДнейДляАнализаТоргов = 7;
	НачЗначTransID = День(ДатаЗаявок) * 1000 + 1;
	СуммаЛимитНаОднуБумагу = 5000;
	ОтступПроц = 0.1;
	ЗащитныйСпрэдПроц = 0.1;
	СрокДействияЗаявки = КонецМесяца(ДатаЗаявок);
	Для Каждого Категория Из Перечисления.КатегорииОблигаций Цикл
		НоваяСтрока = ПлановыеДоходности.Добавить();
		НоваяСтрока.Категория = Категория;
		НоваяСтрока.ПлановаяДоходность = 5; // %
	КонецЦикла; 
	
	
	// {{ условное оформление
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	ГруппаОтбора = ЭлементУО.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ГруппаОтбора.Использование = Истина;
	// + условие ПлановаяЦена > МинимальнаяЦена
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Данные.ПлановаяЦена");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ЭлементОтбора.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Данные.МинимальнаяЦена");
	ЭлементОтбора.Использование = Истина;
	// + условие МинимальнаяЦена <> 0
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Данные.МинимальнаяЦена");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ЭлементОтбора.Использование = Истина;
	// + оформляемое поле
	ОформляемоеПоле = ЭлементУО.Поля.Элементы.Добавить();
	ОформляемоеПоле.Использование = Истина;
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ДанныеПлановаяЦена");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.ЛососьСветлый);
	ЭлементУО.Использование = Истина;
	// условное оформление }}
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеНаСервере()
	
	ДатаОкончанияАнализаТоргов = ДатаЗаявок;
	ДатаНачалаАнализаТоргов = ДатаОкончанияАнализаТоргов - КолвоДнейДляАнализаТоргов * (24*60*60); // за последние N дней
	
	Данные.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.Код КАК Код,
	|	Номенклатура.Наименование КАК Наименование,
	|	Номенклатура.ИдентификаторРежимаТоргов КАК ИдентификаторРежимаТоргов,
	|	Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
	|	Номенклатура.НомерГосРегистрации КАК НомерГосРегистрации,
	|	Номенклатура.ДатаНачалаТоргов КАК ДатаНачалаТоргов,
	|	Номенклатура.ДатаПогашения КАК ДатаПогашения,
	|	Номенклатура.ПервоначальнаяНоминальнаяСтоимость КАК ПервоначальнаяНоминальнаяСтоимость,
	|	Номенклатура.ВалютаНоминала КАК ВалютаНоминала,
	|	Номенклатура.ОбъемВыпуска КАК ОбъемВыпуска,
	|	Номенклатура.НоминальнаяСтоимость КАК НоминальнаяСтоимость,
	|	Номенклатура.ДляКвалифицированныхИнвесторов КАК ДляКвалифицированныхИнвесторов,
	|	Номенклатура.ПериодичностьВыплатыКупона КАК ПериодичностьВыплатыКупона,
	|	Номенклатура.СтавкаКупона КАК СтавкаКупона,
	|	Номенклатура.Категория КАК Категория,
	|	Номенклатура.СтавкаНалогаНаКупонныйДоход КАК СтавкаНалогаНаКупонныйДоход,
	|	Номенклатура.СтавкаНалогаНаПриростКапитала КАК СтавкаНалогаНаПриростКапитала,
	|	Номенклатура.Купоны.(
	|		ДатаНачалаПериода КАК ДатаНачалаПериода,
	|		ДатаВыплаты КАК ДатаВыплаты,
	|		Ставка КАК Ставка,
	|		Сумма КАК Сумма
	|	) КАК Купоны,
	|	Номенклатура.Погашения.(
	|		ДатаПогашения КАК ДатаПогашения,
	|		Сумма КАК Сумма
	|	) КАК Погашения
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ПометкаУдаления
	|	И Номенклатура.ДатаНачалаТоргов <= &ДатаЗаявок
	|	И Номенклатура.ДатаПогашения > &ДатаЗаявок
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПогашения";
	Запрос.УстановитьПараметр("ДатаЗаявок", ДатаЗаявок);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Данные.Добавить(); 
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.ДоПогашения = ОбщегоНазначенияКлиентСервер.РазницаВДнях(ДатаЗаявок, НоваяСтрока.ДатаПогашения);
		// купоны
		Купоны = Выборка.Купоны.Выгрузить();
		КолвоСтрок = Купоны.Количество();
		Для ОбратныйИндекс = 1 По КолвоСтрок Цикл
			СтрокаТаблицы = Купоны[КолвоСтрок - ОбратныйИндекс];
			Если СтрокаТаблицы.ДатаВыплаты < ДатаСделок Тогда 
				Купоны.Удалить(СтрокаТаблицы);
			КонецЕсли; 
		КонецЦикла;
		Купоны.Сортировать("ДатаВыплаты");
		Если Купоны.Количество() > 0 Тогда
			БлижайшийКупон = Купоны[0];
			НоваяСтрока.РазмерБлижайшегоКупона	= БлижайшийКупон.Сумма;
			НоваяСтрока.ДатаБлижайшегоКупона	= БлижайшийКупон.ДатаВыплаты;
			НоваяСтрока.НКД 					= БлижайшийКупон.Сумма / ОбщегоНазначенияКлиентСервер.РазницаВДнях(БлижайшийКупон.ДатаНачалаПериода, БлижайшийКупон.ДатаВыплаты) * ОбщегоНазначенияКлиентСервер.РазницаВДнях(БлижайшийКупон.ДатаНачалаПериода, ДатаСделок);
			НоваяСтрока.ДоБлижайшегоКупона		= ОбщегоНазначенияКлиентСервер.РазницаВДнях(ДатаЗаявок, БлижайшийКупон.ДатаВыплаты);
		КонецЕсли;
		НоваяСтрока.СуммаОставшихсяКупонов = Купоны.Итог("Сумма");
		
		// расчет плановой цены
		НайденныеСтроки = ПлановыеДоходности.НайтиСтроки(Новый Структура("Категория", НоваяСтрока.Категория));
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		Доходность = НайденныеСтроки[0].ПлановаяДоходность / 100;
		КомиссияБрокера = КомиссияБрокераПроцент / 100;
		
		// Доходность = (Доходы - Расходы) / Расходы * 365 / ДоПогашения
		// - или
		// Доходность / 365 * ДоПогашения = Доходы / Расходы - 1
		// - пусть ДоходностьШтрих = Доходность / 365 * ДоПогашения
		ДоходностьШтрих = Доходность / 365 * НоваяСтрока.ДоПогашения;
		// - тогда 
		// ДоходностьШтрих = Доходы / Расходы - 1
		// - т.е.
		// ДоходностьШтрих + 1 = Доходы / Расходы
		// -- или
		// Расходы = Доходы / (ДоходностьШтрих + 1)
		// - при этом
		// Доходы = Номинал + ЧистыйКупон
		Доходы = НоваяСтрока.НоминальнаяСтоимость + НоваяСтрока.СуммаОставшихсяКупонов * (1 - НоваяСтрока.СтавкаНалогаНаКупонныйДоход / 100);
		// - а
		// Расходы = Цена + НКД + (КомиссияБрокера * Цена)
		// - значит
		// (1 + КомиссияБрокера) * Цена + НКД = Доходы / (ДоходностьШтрих + 1)
		// - откуда
		// Цена = (Доходы / (ДоходностьШтрих + 1) - НКД) / (1 + КомиссияБрокера);
		Цена = (Доходы / (ДоходностьШтрих + 1) - НоваяСтрока.НКД) / (1 + КомиссияБрокера);
		
		// если Цена меньше номинала, то вычтем НДФЛ (на самом деле при этом изменится и сумма налога, но в первом приближении нас устроит такая точность)
		Если Цена < НоваяСтрока.НоминальнаяСтоимость Тогда
			Цена = Цена - (НоваяСтрока.НоминальнаяСтоимость - Цена) * НоваяСтрока.СтавкаНалогаНаПриростКапитала / 100;
		КонецЕсли; 
		
		НоваяСтрока.ПлановоеКоличество = Окр(СуммаЛимитНаОднуБумагу / Цена);
		НоваяСтрока.ПлановаяЦена = Цена / НоваяСтрока.НоминальнаяСтоимость * 100; // в %-тах от номинала
		
		// данные по торгам
		ТаблицаТоргов = ПолучениеДанныхМосбиржиСервер.ПолучитьИсториюТорговПоОблигации(
							НоваяСтрока.Код, 
							НоваяСтрока.ИдентификаторРежимаТоргов, 
							ДатаНачалаАнализаТоргов, 
							ДатаОкончанияАнализаТоргов);
		КоличествоСтрок = ТаблицаТоргов.Количество();
		Если КоличествоСтрок > 0 Тогда
			МинЦена = Неопределено;
			МаксЦена = Неопределено;
			Объем = 0;
			КолвоСделок = 0;
			Для Каждого СтрокаТаблицы Из ТаблицаТоргов Цикл
				Если ЗначениеЗаполнено(СтрокаТаблицы.Минимум) Тогда
					МинЦена = ?(МинЦена = Неопределено, СтрокаТаблицы.Минимум, Мин(МинЦена, СтрокаТаблицы.Минимум));
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаТаблицы.Максимум) Тогда
					МаксЦена = ?(МаксЦена = Неопределено, СтрокаТаблицы.Максимум, Макс(МаксЦена, СтрокаТаблицы.Максимум));
				КонецЕсли; 
				Объем = Объем + СтрокаТаблицы.Объем;
				КолвоСделок = КолвоСделок + СтрокаТаблицы.КоличествоСделок;
			КонецЦикла;
			НоваяСтрока.МинимальнаяЦена = МинЦена;
			НоваяСтрока.МаксимальнаяЦена = МаксЦена;
			НоваяСтрока.ЦенаЗакрытия = ТаблицаТоргов[КоличествоСтрок - 1].Закрытие;
			НоваяСтрока.ДатаПоследнейСделки = ТаблицаТоргов[КоличествоСтрок - 1].ДатаПоследнейСделки;
			НоваяСтрока.СреднийОбъем = Объем / КоличествоСтрок;
			НоваяСтрока.СреднееКоличествоСделок = КолвоСделок / КоличествоСтрок;
			
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанные(Команда)
	ЗаполнитьДанныеНаСервере();
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаРезультат;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьДатуСделок()
	
	Сутки =  24 * 60 * 60;
	ДатаСделок = ДатаЗаявок + (ТоргиВРежимеТПлюс * Сутки); // Т+
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗаявокПриИзменении(Элемент)
	
	РассчитатьДатуСделок();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоргиВРежимеТПлюсПриИзменении(Элемент)
	
	РассчитатьДатуСделок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РассчитатьДатуСделок();
		
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЗаявки(Команда)
	
	ФорматЦены			= "ЧРД=.; ЧГ=";
	ФорматКоличества	= "ЧРД=.; ЧГ=";
	ФорматДаты			= "ДФ=yyyyMMdd";
	
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла);
	
	ИдТранзакции = НачЗначTransID;
	
	Для Каждого СтрокаТаблицы Из Данные Цикл
		
		ЦенаПокупки	= СтрокаТаблицы.ПлановаяЦена;
		Количество	= СтрокаТаблицы.ПлановоеКоличество;
		КодКласса	= СтрокаТаблицы.ИдентификаторРежимаТоргов;
		
		Стр = "TRANS_ID="		+ Формат(ИдТранзакции, "ЧГ=") + ";"
			+ "ACCOUNT="		+ Счет + ";"
			+ "CLIENT_CODE="	+ КодКлиента + ";"
			+ "CLASSCODE="		+ КодКласса + ";"
			+ "SECCODE="		+ СтрокаТаблицы.Код + ";"
			+ "OPERATION="		+ "B" + ";"	// buy
			+ "QUANTITY="		+ Формат(Количество, ФорматКоличества) + ";"
		;
		
		Если ВидЗаявки = "Лимитированная" Тогда
			Стр = Стр	
				+ "ACTION="					+ "NEW_ORDER" + ";"
				+ "TYPE="					+ "L" + ";"	// limit
				+ "PRICE="					+ Формат(ЦенаПокупки, ФорматЦены) + ";"
			;	
			
		ИначеЕсли ВидЗаявки = "Тэйк-профит" Тогда
			
			Стр = Стр
				+ "ACTION="					+ "NEW_STOP_ORDER" + ";"
				+ "STOP_ORDER_KIND="		+ "TAKE_PROFIT_STOP_ORDER" + ";"
				+ "STOPPRICE="				+ Формат(ЦенаПокупки, ФорматЦены) + ";"
				+ "EXPIRY_DATE="			+ Формат(СрокДействияЗаявки, ФорматДаты) + ";"
				+ "OFFSET="					+ Формат(ОтступПроц, ФорматЦены) + ";"
				+ "OFFSET_UNITS="			+ "PERCENTS" + ";"
				+ "SPREAD="					+ Формат(ЗащитныйСпрэдПроц, ФорматЦены) + ";"
				+ "SPREAD_UNITS="			+ "PERCENTS" + ";"
			;	
			
		ИначеЕсли ВидЗаявки = "Стоп-цена по другой бумаге" Тогда
			
			Стр = Стр
				+ "ACTION="					+ "NEW_STOP_ORDER" + ";"
				+ "STOP_ORDER_KIND="		+ "CONDITION_PRICE_BY_OTHER_SEC" + ";"
				+ "STOPPRICE_CLASSCODE="	+ КодКласса + ";"
				+ "STOPPRICE_SECCODE="		+ СтрокаТаблицы.Код + ";"
				+ "STOPPRICE_CONDITION="	+ "<=" + ";"
				+ "STOPPRICE="				+ Формат(ЦенаПокупки, ФорматЦены) + ";"
				+ "PRICE="					+ Формат(ЦенаПокупки, ФорматЦены) + ";"
				+ "EXPIRY_DATE="			+ Формат(СрокДействияЗаявки, ФорматДаты) + ";"
			;	
			
		КонецЕсли; 
		
		ЗаписьТекста.ЗаписатьСтроку(Стр);
		
		ИдТранзакции = ИдТранзакции + 1;
		
	КонецЦикла; 
	
	ЗаписьТекста.Закрыть();
	
	НачЗначTransID = ИдТранзакции;
	
КонецПроцедуры
